apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'

repositories {
    mavenCentral()
    jcenter()
}

gradlePlugin {
    plugins {
        distributedTestingPlugin {
            id = 'com.r3.testing.distributed-testing'
            implementationClass = 'com.r3.testing.DistributedTesting'
        }
        imageBuildingPlugin {
            id = 'com.r3.testing.image-building'
            implementationClass = 'com.r3.testing.ImageBuilding'
        }
    }
    automatedPublishing = false
}

dependencies {
    implementation "io.fabric8:kubernetes-client:4.4.1"
    implementation 'org.apache.commons:commons-compress:1.19'
    implementation "io.github.classgraph:classgraph:4.8.53"
    implementation "com.bmuschko:gradle-docker-plugin:5.0.0"
    implementation 'org.apache.commons:commons-csv:1.1'
    implementation group: 'org.jetbrains', name: 'annotations', version: '13.0'
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.5.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.5.2"
}

tasks.withType(Test) {
    useJUnitPlatform()

    // Prevent the project from creating temporary files outside of the build directory.
    systemProperty 'java.io.tmpdir', buildDir.absolutePath

    // Tell the tests where Gradle's current module cache is.
    // We need the tests to share this module cache to prevent the
    // Gradle Test-Kit from downloading its own copy of Kotlin etc.
    systemProperty 'test.gradle.user.home', project.gradle.gradleUserHomeDir
}

wrapper {
    gradleVersion = "5.6.4"
    distributionType = Wrapper.DistributionType.ALL
}